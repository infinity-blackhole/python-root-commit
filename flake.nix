{
  description = "Python template project";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/release-22.11";
    devenv = {
      url = "github:cachix/devenv";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  nixConfig = {
    extra-public-keys = [
      "devenv.cachix.org-1:w1cLUi8dv3hnoSPGAuibQv+f9TZLr6cv/Hm9XgU50cw="
    ];
    extra-substituters = [
      "https://devenv.cachix.org"
    ];
  };

  outputs = { nixpkgs, devenv, ... }@inputs: {
    devShells = nixpkgs.lib.genAttrs nixpkgs.lib.platforms.unix (system:
      let pkgs = import nixpkgs { inherit system; }; in {
        default = devenv.lib.mkShell {
          inherit inputs pkgs;
          modules = [
            {
              pre-commit.hooks = {
                actionlint.enable = true;
                markdownlint.enable = true;
                shellcheck.enable = true;
                shfmt.enable = true;
                nixpkgs-fmt.enable = true;
                statix.enable = true;
                deadnix.enable = true;
                hadolint.enable = true;
              };
              packages = [
                pkgs.nixpkgs-fmt
                pkgs.docker
              ];
            }
            {
              pre-commit.hooks = {
                black.enable = true;
                isort.enable = true;
              };
              packages = [
                (pkgs.python3.withPackages (ps: with ps; [
                  pip
                  isort
                  black
                  hatchling
                ]))
              ] ++ pkgs.lib.optionals pkgs.stdenv.isLinux [
                # TODO: https://github.com/NixOS/nixpkgs/issues/209358
                pkgs.hatch
              ];
            }
          ];
        };
      }
    );
  };
}
